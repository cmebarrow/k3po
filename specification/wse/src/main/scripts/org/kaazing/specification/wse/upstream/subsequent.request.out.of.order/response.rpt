property upstreamId ${wse:uniqueId()}
property downstreamId ${wse:uniqueId()}
property upstream ${wse:append("http://localhost:8080/path/", upstreamId)}
property downstream ${wse:append("http://localhost:8080/path/", downstreamId)}

accept http://localhost:8080/path/;e/cb?query
accepted
connected

read method "POST"
read version "HTTP/1.1"
read header "X-WebSocket-Version" "wseb-1.0"
read header "X-Accept-Commands" "ping"
read header "X-Sequence-No" /(?<sequence>[0-9]{1,3})/
read closed

write status "201" "Created" 
write version "HTTP/1.1"
write header "X-WebSocket-Version" "wseb-1.0"
write header "Content-Type" "text/plain;charset=utf-8"
write header content-length

write ${upstream} "\n"
write ${downstream} "\n"
write notify CREATE_COMPLETED
write close

# Downstream
accept ${downstream}
accepted
connected

read await CREATE_COMPLETED
read method "GET"
read version "HTTP/1.1"
read header "X-Sequence-No" ${wse:asString(sequence + 1)}
read closed

write status "200" "OK"
write version "HTTP/1.1"
write header "Content-Type" "application/octet-stream"
write header "Connection" "close"
write flush

write await FIRST_PING_RECEIVED
write [0x8A 0x00]

# Upstream
accept ${upstream}
accepted
connected

read await CREATED
read method "POST"
read version "HTTP/1.1"
read header "Content-Type" "application/octet-stream"
read header "X-Sequence-No" ${wse:asString(sequence + 1)}

read [0x89 0x00]
read [0x01 0x30 0x31 0xFF]
read closed
read notify FIRST_PING_RECEIVED

write status "200" "OK"
write version "HTTP/1.1"
write header content-length
write close

# Upstream Out of Order
accepted
connected

read await FIRST_PING_RECEIVED
read method "POST"
read version "HTTP/1.1"
read header "Content-Type" "application/octet-stream"
read header "X-Sequence-No" ${wse:asString(sequence + 3)}

read [0x89 0x00]
read [0x01 0x30 0x31 0xFF]
read closed
read notify OUT_OF_ORDER_REQUEST_RECEIVED

write status "400" "Bad Request" 
write header content-length
write close
